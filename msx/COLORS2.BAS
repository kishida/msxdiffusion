100 GET TIME ZS$
110 DEFINT A-Y
120 SCREEN 8
130 LINE (0,0)-(0,191),255
140 DIM YR(256,1),YG(256,1)
150 FOR Y=0 TO 191
160 CR=Y MOD 2:CW=1-CR
170 DR=YR(0,CR)+(Y MOD 96):DG=YG(0,CR)
180 B1=INT(Y/96)*2
190 R=INT(DR/12):G=INT(DG/16)
200 IF R>7 THEN R=7
210 IF G>7 THEN G=7
220 DR=DR-R*12:DG=DG-G*16
230 TR=DR*6/16:YR(0,CW)=TR:TG=DG*6/16:YG(0,CW)=TG
240 T=DR/16:YR(1,CW)=T:DR=DR-TR-T
250 T=DG/16:YG(1,CW)=T:DG=DG-TG-T
260 PSET(0,Y),G*32+R*4+B1
270 FOR X=1 TO 255
280 DR=DR+YR(X,CR)+(Y MOD 96):DG=DG+YG(X,CR)+(X MOD 128)
290 R=INT(DR/12):G=INT(DG/16)
300 IF R>7 THEN R=7
310 IF G>7 THEN G=7
320 DR=DR-R*12:DG=DG-G*16
330 T=INT(DR/16):YR(X+1,CW)=T:DR=DR-T
340 T=INT(DG/16):YG(X+1,CW)=T:DG=DG-T
350 T=INT(DR/3):YR(X,CW)=YR(X,CW)+T:DR=DR-T
360 T=INT(DG/3):YG(X,CW)=YG(X,CW)+T:DG=DG-T
370 T=INT(DR*3/10):YR(X-1,CW)=YR(X-1,CW)+T:DR=DR-T
380 T=INT(DG*3/10):YG(X-1,CW)=YG(X-1,CW)+T:DG=DG-T
390 PSET(X,Y),G*32+R*4+B1+INT(X/128)
400 NEXT X
410 NEXT Y
420 GET TIME ZE$
430 BSAVE "diff.sc8", 0, &HD3FF,S
440 A$=INPUT$(1)
450 SCREEN 0
460 PRINT ZS$
470 PRINT ZE$
